buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath(group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true) { exclude group: 'org.apache.logging.log4j' }
        classpath 'org.apache.logging.log4j:log4j-api:2.11.2'
        classpath 'org.apache.logging.log4j:log4j-core:2.11.2'
        classpath 'org.parchmentmc:librarian:1.+'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
    //    id "org.sonarqube" version "3.3"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'

//sonarqube {
//    properties {
//        property "sonar.projectKey", "AlmostReliable_templatemod"
//        property "sonar.organization", "almostreliable"
//        property "sonar.host.url", "https://sonarcloud.io"
//    }
//}

version = "${mc_version}-${mod_version}"
group = "${mod_base}.${mod_id}"
archivesBaseName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            // AE2 mixin fix
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg".toString()

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            // AE2 mixin fix
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg".toString()

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            // AE2 mixin fix
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg".toString()

            // data generation
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

// resources from data generation
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name "Modmaven" // Applied Energistics
        url "https://modmaven.dev/"
        content {
            includeGroup 'appeng'
        }
    }
    maven {
        name "Progwml6 maven" // Just Enough Items
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name 'BlameJared' // CraftTweaker
        url 'https://maven.blamejared.com'
    }
    maven {
        name 'CurseMaven'
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        url = "https://maven.architectury.dev"
    }
    maven {
        url "https://maven.saps.dev/minecraft"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    // required dependencies
    implementation fg.deobf("appeng:appliedenergistics2:${ae2_version}")
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-${mc_version}:${crt_version}")

    implementation fg.deobf("dev.latvian.mods:kubejs-forge:${kubejs_version}")
    implementation fg.deobf("dev.architectury:architectury-forge:${architectury_version}")
    implementation fg.deobf("dev.latvian.mods:rhino-forge:${rhino_version}")

    // runtime dependencies
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}") // JEI - in sync with API version
    runtimeOnly fg.deobf("curse.maven:top-245211:3671753") // TOP - 5.1.0
    runtimeOnly fg.deobf("curse.maven:ding-231275:3548400") // Ding - 1.3.0
    runtimeOnly fg.deobf("curse.maven:solar-336538:3770535") // Solar Generation - 4.1.1
    // runtimeOnly fg.deobf("curse.maven:solar-381462:3569092") // Solar Energy - 0.4.0.3
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
    debug.export = true
}

processResources {
    def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
    def replaceProperties = [
        mod_id           : mod_id,
        mod_name         : mod_name,
        mod_version      : mod_version,
        mod_authors      : mod_authors,
        mod_description  : mod_description,
        mod_credits      : mod_credits,
        mc_version_range : mc_version_range,
        forge_min_version: forge_min_version,
        license          : license,
        github_user      : github_user,
        github_repo      : github_repo,
        ae2_version_range: ae2_version_range,
        jei_version_range: jei_version_range,
        crt_version_range: crt_version_range
    ]

    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

jar {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : mod_version,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }
}

curseforge {
    if (System.getenv('CF_API_KEY') != null) {
        // GitHub actions
        apiKey = System.getenv('CF_API_KEY')
    } else {
        // error prevention
        apiKey = "none"
    }
    def RELEASE_TYPE
    if (System.getenv('RELEASE_TYPE') == null || System.getenv('RELEASE_TYPE') == '') {
        RELEASE_TYPE = "release"
    } else {
        RELEASE_TYPE = System.getenv('RELEASE_TYPE')
    }
    project {
        id = project_id
        changelogType = 'markdown'
        changelog = "The whole changelog can be found [here](https://github.com/${github_user}/${github_repo}/blob/${github_branch}/CHANGELOG.md)."
        releaseType = RELEASE_TYPE
        addGameVersion mc_version

        mainArtifact(jar) {
            if (RELEASE_TYPE == 'release') {
                displayName = "${mod_name}-${mc_version}-${mod_version}"
            } else {
                displayName = "${mod_name}-${mc_version}-${mod_version}-${RELEASE_TYPE}"
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
jar.finalizedBy('reobfJar')
